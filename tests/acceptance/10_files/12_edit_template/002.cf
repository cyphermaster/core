#######################################################
#
# Expand Template on a block based template file 
#
#######################################################

body common control
{
  inputs => { "../../default.cf.sub" };
  bundlesequence  => { default("$(this.promise_filename)") };
  version => "1.0";
nova_edition::
  host_licenses_paid => "5";
}

bundle agent init
{
vars:
        "states" slist => { "expected", "template" };

        "expected" string =>
"
globals {
  send_metadata_interval = 0 /*secs */
}
cluster {
  url = \"unspecified\"
}
host {
  location = \"unspecified\"
}";

        "template" string =>
"
[%CFEngine BEGIN %]
globals {
  send_metadata_interval = 0 /*secs */
}
cluster {
  url = \"unspecified\"
}
host {
  location = \"unspecified\"
}
[%CFEngine END %]";

files:
        "$(G.testfile).$(states)"
            create => "true",
            edit_line => init_insert("$(init.$(states))"),
            edit_defaults => init_empty;
}

bundle edit_line init_insert(str)
{
insert_lines:
        "$(str)";
}

body edit_defaults init_empty
{
        empty_file_before_editing => "true";
}

bundle agent test
{
files:
        "$(G.testfile).actual",
            create  =>  "true",
            edit_template => "$(G.testfile).template";
}

bundle agent check
{
methods:
        "any" usebundle => default_check_diff("$(G.testfile).actual",
                                              "$(G.testfile).expected",
                                              "$(this.promise_filename)");
}

